
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Товары.Итог("Результат") + Услуги.Итог("Результат");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ЗадолженностьПоставщикам.Записывать = Истина;
	Движения.Затраты.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		
		Движение = Движения.Затраты.Добавить();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Результат = ТекСтрокаТовары.Результат;
		
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Результат = ТекСтрокаТовары.Результат;
		Движение.Количество = ТекСтрокаТовары.Количество;
		
	КонецЦикла;

	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		Движение = Движения.Затраты.Добавить();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаУслуги.Номенклатура;
		Движение.Результат = ТекСтрокаУслуги.Результат;
	КонецЦикла;
 
	Движение = Движения.ЗадолженностьПоставщикам.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Результат = СуммаДокумента;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
